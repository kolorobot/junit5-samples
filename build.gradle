buildscript {
    ext {
        junitPlatformVersion = junitPlatformVersion
        junitJupiterVersion = junitJupiterVersion
        kotlinVersion = kotlinVersion
    }
    repositories {
        mavenCentral()
    }
}

allprojects {

    group = 'pl.codeleak.samples.junit5-samples'
    version = "${junitPlatformVersion}"

    apply plugin: 'java'

    sourceCompatibility = 11

    repositories {
        mavenCentral()
    }

    dependencies {
        /* The only one required dependency to get started with JUnit 5 */
        testCompile("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

        /* To avoid compiler warnings about @API annotations in JUnit code */
        testCompileOnly('org.apiguardian:apiguardian-api:1.0.0')
    }


    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        reports {
            html.enabled = true
        }
    }
}


project(":shared") {
    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.2")
    }
}

configure(subprojects.findAll { it.name != 'shared' }) {
    dependencies {
        compile project(":shared")
    }
}

wrapper {
    gradleVersion = '5.2.1'
}
